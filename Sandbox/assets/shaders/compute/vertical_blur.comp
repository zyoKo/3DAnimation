#version 460 core

layout (local_size_x = 1, local_size_y = 128, local_size_z = 1) in;

layout (binding = 0) uniform blurKernel
{ 
	float weights[101];
};

layout (rgba32f, binding = 1) uniform readonly image2D sourceImage;
layout (rgba32f, binding = 2) uniform writeonly image2D destinationImage;

//uniform int halfSize; // half-size of kernel

// Variable shared with other threads in the 128x1 thread group
shared vec4 v[128 + 101];

void main()
{
	int halfSize = 50;

	// image size
	ivec2 bufferSize = imageSize(sourceImage);

	// Combo of groupID, groupSize and localID
	ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);

	// Local thread id in the 128x1 thread groups128x1
	uint i = gl_LocalInvocationID.y;

	// what pixel to read and don't read beyond texture
	ivec2 pixel = texelCoord + ivec2(0, -halfSize);
	pixel.y = int(max(0, min(pixel.y, bufferSize.y - 1)));

	v[i] = imageLoad(sourceImage, pixel);
	
	if (i < (2 * halfSize))
	{
		pixel = texelCoord + ivec2(0, 128 - halfSize);
		pixel.y = int(max(0, min(pixel.y, bufferSize.y - 1)));

		v[i + 128] = imageLoad(sourceImage, pixel);
	}

	barrier();

	vec4 sum = vec4(0.0);
	for(int j = 0; j < (2 * halfSize + 1); j++)
	{
		sum += weights[j] * v[i + j];
	}

	imageStore(destinationImage, texelCoord, sum);
}